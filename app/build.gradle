plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Upload local.properties variables
def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace 'com.example.comics'
    compileSdk 35

    defaultConfig {
        applicationId "com.example.comics"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        // Variables from local.properties
        buildConfigField "String", "BASE_URL", "\"${localProperties.getProperty('BASE_URL')}\""
        buildConfigField "String", "MARVEL_API_KEY", "\"${localProperties.getProperty('MARVEL_API_KEY')}\""
        buildConfigField "String", "MARVEL_TS", "\"${localProperties.getProperty('MARVEL_TS')}\""
        buildConfigField "String", "MARVEL_HASH", "\"${localProperties.getProperty('MARVEL_HASH')}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation(libs.androidx.core)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.constraintlayout)
    implementation(libs.gson)
    implementation(libs.coroutines.core)
    implementation(libs.glide)
    implementation(libs.retrofit)
    implementation(libs.retrofit.converter.gson)
    implementation(libs.lifecycle.viewmodel.ktx)
    implementation(libs.lifecycle.livedata.ktx)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.lifecycle.common.java8)
    implementation(libs.swiperefreshlayout)
    implementation(libs.koin.android)

    testImplementation(libs.junit)
    testImplementation(libs.mockk)
    testImplementation(libs.coroutines.test)
    testImplementation(libs.arch.core.testing)

    androidTestImplementation(libs.androidx.test.ext.junit)
    androidTestImplementation(libs.espresso.core)
}